<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" width="400" height="300" creationComplete="init()" title="Reset your password">


<mx:Script>
	<![CDATA[
		import org.systemsbiology.echidna.ui.EchidnaAjaxFault;
		import org.systemsbiology.echidna.events.ReloginEvent;
		import org.systemsbiology.echidna.events.ResetPasswordEvent;
		import mx.managers.PopUpManager;
		import org.systemsbiology.echidna.events.GotLoginInfoEvent;
		import mx.core.IFlexDisplayObject;
		import mx.rpc.events.ResultEvent;
		import mx.validators.Validator;
		import org.systemsbiology.echidna.common.Util;
		import mx.controls.Alert;
		
		private var validators:Array = new Array();
		
		private var ajaxFault:EchidnaAjaxFault;
	
		protected function init():void {
			ajaxFault = new EchidnaAjaxFault(this);
			validators.push(password_val);
			validators.push(confirm_val);
			passwordText.setFocus();
		}
		
		public function set email(value:String):void {
			emailText.text = value;
		}
		
		public var token:String;
		
		protected function changePassword():void {
			var savedThis:IFlexDisplayObject = this;
			var validatorErrorArray:Array = Validator.validateAll(validators);
			var isValidForm:Boolean = validatorErrorArray.length == 0;
			if (!isValidForm) {
				Alert.show("Please fill out all fields.");
				return;
			}
			if (passwordText.text != confirmPasswordText.text) {
				Alert.show("Passwords do not match!");
				return;
			}
			Util.ajax("main/login",
			 {"email": emailText.text, "password": passwordText.text, "reset_password": "true", "token": token},
			 function(event:ResultEvent):void {
			 	dispatchResetPasswordEvent();
				PopUpManager.removePopUp(savedThis);
			}, ajaxFault.ajaxFault, "POST");
		}
		
		protected function dispatchResetPasswordEvent():void {
		 	var rspe:ResetPasswordEvent = new ResetPasswordEvent(ResetPasswordEvent.RESET_PASSWORD_EVENT);
		 	rspe.email = emailText.text;
		 	dispatchEvent(rspe);
		}
		
		protected function logIn():void {
			dispatchResetPasswordEvent();
			var evt:ReloginEvent = new ReloginEvent(ReloginEvent.RELOGIN_EVENT);
			dispatchEvent(evt);
			PopUpManager.removePopUp(this);
		}
		
	]]>
</mx:Script>	
	<mx:Label x="19" y="36" text="Email:"/>
	<mx:Text x="67" y="36" width="261" id="emailText"/>
	<mx:Label x="15" y="62" text="New Password:"/>
	<mx:Label x="15" y="88" text="Confirm new password:"/>
	<mx:TextInput x="168" y="60" displayAsPassword="true" id="passwordText" enter="changePassword()"/>
	<mx:TextInput x="168" y="86" displayAsPassword="true" id="confirmPasswordText" enter="changePassword()"/>
	<mx:Button x="15" y="126" label="Change Password and Log In" click="changePassword()"/>
	<mx:StringValidator id="password_val" source="{passwordText}" property="text" minLength="1"/>
	<mx:StringValidator id="confirm_val" source="{confirmPasswordText}" property="text" minLength="1"/>
	<mx:Button x="212" y="126" label="Log In" id="logInButton" toolTip="Just log in, don't change password" click="logIn()"/>
</mx:TitleWindow>
