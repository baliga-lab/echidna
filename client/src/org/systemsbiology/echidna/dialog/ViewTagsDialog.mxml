<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" width="456" height="508" title="View Tags">
	<mx:Script>
		<![CDATA[
			import com.adobe.serialization.json.JSON;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import mx.events.ListEvent;
			import mx.managers.PopUpManager;
			import mx.rpc.events.ResultEvent;
			
			import org.systemsbiology.echidna.common.Util;
			import org.systemsbiology.echidna.events.RenameTagEvent;
			import org.systemsbiology.echidna.ui.EchidnaAjaxFault;
			
			[Bindable]
			private var _tagsAC:ArrayCollection = new ArrayCollection();
			private var allTags:ArrayCollection;
			
			private var ajaxFault:EchidnaAjaxFault = new EchidnaAjaxFault();
			
			
			
			public function set tagsAC(tagsAC:ArrayCollection):void {
				_tagsAC = tagsAC;
				allTags = tagsAC;
			}
			
			public function toggle():void {
				if (toggleAutoButton.label == "Hide Auto Tags") {
					var tmpAC:ArrayCollection = new ArrayCollection();
					for(var i:int = 0; i < allTags.length; i++) {
						if (!allTags[i]['auto']) {
							tmpAC.addItem(allTags[i]);
						}
					}
					_tagsAC = tmpAC;
					toggleAutoButton.label = "Show Auto Tags";
				} else {
					_tagsAC = allTags;
					toggleAutoButton.label = "Hide Auto Tags";
				}
			}
			
			

			protected function tagsList_itemClickHandler(event:ListEvent):void {
				var enabled:Boolean = (tagsList.selectedItems.length == 1 && !tagsList.selectedItem['auto']);
				deleteTagButton.enabled = enabled;
				renameTagButton.enabled = enabled;
				
				viewResultsButton.enabled = (tagsList.selectedItems.length > 0);
			}
			
			public function getTags():void {
				Util.ajax("main/get_tags", null, function(event:ResultEvent):void {
					var obj:Object = JSON.decode(event.result.toString());
					tagsAC = Util.objectToArrayCollection(obj, "tag");
				}, ajaxFault.ajaxFault);
			}
			
			protected function deleteTag():void {
				var tagToDelete:String = tagsList.selectedItem['tag'];
				Alert.show("Are you sure you want to delete the tag '" + tagToDelete + "'?", 
					 "Confirm Delete", Alert.YES|Alert.NO, this, function(event:CloseEvent):void {
						 if (event.detail == Alert.YES) {
							 Util.ajax("main/delete_tag", {tag: tagToDelete}, function(event:ResultEvent):void {
								 getTags();
							 }, ajaxFault.ajaxFault);
						 }
					 }, null, Alert.NO);
			}
			
			protected function renameTag():void {
				var rtd:RenameTagDialog = new RenameTagDialog();
				var oldName:String = tagsList.selectedItem['tag'];
				trace("oldName = " + oldName);
				PopUpManager.addPopUp(rtd, this, true);
				PopUpManager.centerPopUp(rtd);
				rtd.oldName = oldName;
				rtd.existingTags = allTags;
				rtd.addEventListener(RenameTagEvent.RENAME_TAG_EVENT, function(event:RenameTagEvent):void {
					Util.ajax("main/rename_tag", {old_name: oldName, new_name: event.newName}, function(event:ResultEvent):void {
						getTags();
					}, ajaxFault.ajaxFault);
				});
			}
			
			protected function closeDialog():void {
				PopUpManager.removePopUp(this);
			}
			
			protected function viewResults():void {
				var selectedTags:Array = new Array();
				for(var i:int = 0; i < tagsList.selectedItems.length; i++) {
					selectedTags.push(tagsList.selectedItems[i]['tag']);
				}
				var savedThis:TitleWindow = this;
				var tagsString:String = selectedTags.join(",");
				Util.ajax("main/get_groups_by_tags", {tags: tagsString}, function(event:ResultEvent):void {
					PopUpManager.removePopUp(savedThis);
					dispatchEvent(event);
				}, ajaxFault.ajaxFault);
			}

		]]>
	</mx:Script>
	
	<mx:List x="31" y="51" id="tagsList" dataProvider="{_tagsAC}" allowMultipleSelection="true" labelField="tag" width="242" height="244" itemClick="tagsList_itemClickHandler(event)">
		<mx:itemRenderer>
			<mx:Component>
				<mx:Label text="{data.tag}">
					<mx:Script>
						<![CDATA[
							override public function set data( value:Object ) : void {
								super.data = value;
								if( data['auto'] ) setStyle("fontWeight","normal");
								else setStyle("fontWeight","bold");
							}
						]]>
					</mx:Script>
				</mx:Label>
			</mx:Component>
		</mx:itemRenderer>
	</mx:List> 
	<mx:Button x="31" y="318" label="Hide Auto Tags" id="toggleAutoButton" click="toggle()"/>
	<mx:Label x="31" y="25" text="Manually created tags are listed in bold below:"/>
	<mx:Button x="31" y="414" label="View Results" id="viewResultsButton" click="viewResults()" enabled="false"/>
	<mx:Button x="162" y="318" label="Delete" id="deleteTagButton" click="deleteTag()" enabled="false"/>
	<mx:Button x="234" y="318" label="Rename" id="renameTagButton" click="renameTag()" enabled="false"/>
	<mx:Button x="137" y="414" label="Close" id="closeDialogButton" click="closeDialog()"/>
	
</mx:TitleWindow>
